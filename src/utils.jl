"""
    innereltype(x)

Recursively determine the 'innermost' type in by the collection `x` (which may be, for example,
a collection of a collection).
"""
function innereltype(x)
  T = eltype(x)

  return T <: AbstractArray ? innereltype(T) : return T
end

"""
    cxtype(T)

Returns `T` when `T` is `Complex`, or `Complex{T}` when `T` is `Real`.
"""
cxtype(::Type{T}) where T<:Number = T
cxtype(::Type{T}) where T<:Real = Complex{T}

"""
    fltype(T)

Returns `T` when `T<:AbstractFloat` or `Tf` when `T<:Complex{Tf}`.
"""
fltype(::Type{T})          where T<:AbstractFloat = T
fltype(::Type{Complex{T}}) where T<:AbstractFloat = T
fltype(T::Tuple) = fltype(T[1])

"""
    superzeros(T, A)

Returns an array like `A`, but full of zeros. If `innereltype(A)` can be promoted to `T`, then
the innermost elements of the array will have type `T`.
"""
superzeros(T, A::AbstractArray) = T(0) * A
superzeros(A::AbstractArray) = superzeros(innereltype(A), A)
superzeros(T, dims::Tuple) = eltype(dims) <: Tuple ? [ superzeros(T, d) for d in dims ] : zeros(T, dims)
superzeros(dims::Tuple) = superzeros(Float64, dims) # default
superzeros(T::Tuple, dims::Tuple) = [ superzeros(T[i], dims[i]) for i=1:length(dims) ]

"""
    @superzeros T a b c d...
    @superzeros T dims b c d...

Generate arrays `b, c, d...` with the super-dimensions of `a` and innereltype `T`.
"""
macro superzeros(T, ad, vars...)
  expr = Expr(:block)
  append!(expr.args, [:( $(esc(var)) = superzeros($(esc(T)), $(esc(ad))); ) for var in vars])
  
  return expr
end

supersize(a) = Tuple([size(ai) for ai in a])
supersize(a::Array{T}) where T<:Number = size(a)

macro createarrays(T, dims, vars...)
  expr = Expr(:block)
  append!(expr.args, [:( $(esc(var)) = zeros($(esc(T)), $(esc(dims))); ) for var in vars])
  
  return expr
end

"""
    @zeros T dims a b c...

Create arrays of all zeros with element type `T`, size `dims`, and global names
`a`, `b`, `c` (for example). An arbitrary number of arrays may be created.
"""
macro zeros(T, dims, vars...)
  expr = Expr(:block)
  append!(expr.args, [:( $(esc(var)) = zeros($(esc(T)), $(esc(dims))); ) for var in vars])
  
  return expr
end

Base.zeros(::CPU, T, dims) = zeros(T, dims)

"""
    devzeros(dev, T, dims)

Returns an array like `A` of type `T`, but full of zeros.
"""
devzeros(dev, T, dims) = zeros(dev, T, dims)


"""
    @devzeros dev T dims a b c...

Create arrays of all zeros with element type `T`, size `dims`, and global names
`a`, `b`, `c` (for example) on device `dev`.
"""
macro devzeros(dev, T, dims, vars...)
  expr = Expr(:block)
  append!(expr.args, [:( $(esc(var)) = zeros($(esc(dev))(), $(esc(T)), $(esc(dims))); ) for var in vars])
  
  return expr
end


"""
    parsevalsum2(uh, grid)

Return `Œ£ |uh|¬≤` on the `grid`, which is equal to the domain integral of `u`. More specifically, 
it returns
```math
\\sum_{ùê§} |uÃÇ_{ùê§}|¬≤ L_x L_y = \\int u(ùê±)¬≤ \\, ùñΩx ùñΩy \\,,
```
where ``uÃÇ_{ùê§} =`` `uh` ``/(`` `grid.nx` ``e^{- i ùê§ ‚ãÖ ùê±‚ÇÄ})``, with ``ùê±‚ÇÄ`` the vector with components
the left-most position in each direction.
"""
function parsevalsum2(uh, grid::TwoDGrid)
  if size(uh, 1) == grid.nkr # uh is in conjugate symmetric form
    U = sum(abs2, uh[1, :])           # k=0 modes
    U += 2*sum(abs2, uh[2:end, :])    # sum k>0 modes twice
  else # count every mode once
    U = sum(abs2, uh)
  end

  norm = grid.Lx * grid.Ly / (grid.nx^2 * grid.ny^2) # normalization for dft

  return norm * U
end

function parsevalsum2(uh, grid::OneDGrid)
  if size(uh, 1) == grid.nkr                 # uh is conjugate symmetric
    U = sum(abs2, CUDA.@allowscalar uh[1])   # k=0 modes
    U += @views 2 * sum(abs2, uh[2:end])     # sum k>0 modes twice
  else # count every mode once
    U = sum(abs2, uh)
  end
  
  norm = grid.Lx / grid.nx^2 # normalization for dft
  
  return norm * U
end

"""
    parsevalsum(uh, grid)

Return `real(Œ£ uh)` on the `grid`, i.e.
```math
‚Ñú [ \\sum_{ùê§} uÃÇ_{ùê§} L_x L_y ] \\,,
```
where ``uÃÇ_{ùê§} =`` `uh` ``/(`` `grid.nx` ``e^{- i ùê§ ‚ãÖ ùê±‚ÇÄ})``, with ``ùê±‚ÇÄ`` the vector with components
the left-most position in each direction.
"""
function parsevalsum(uh, grid::TwoDGrid)
  if size(uh, 1) == grid.nkr    # uh is conjugate symmetric
    U = sum(uh[1, :])           # k=0 modes
    U += 2*sum(uh[2:end, :])    # sum k>0 modes twice
  else # count every mode once
    U = sum(uh)
  end

  norm = grid.Lx * grid.Ly / (grid.nx^2 * grid.ny^2) # normalization for dft

  return norm * real(U)
end

"""
    jacobianh(a, b, grid)

Return the Fourier transform of the Jacobian of `a` and `b` on `grid`.
"""
function jacobianh(a, b, grid::TwoDGrid)
  if eltype(a) <: Real
    bh = rfft(b)
    bx = irfft(im * grid.kr .* bh, grid.nx)
    by = irfft(im * grid.l  .* bh, grid.nx)
    
    return im * grid.kr .* rfft(a .* by) .- im * grid.l .* rfft(a .* bx)
  else
    # J(a, b) = ‚àÇ(a * ‚àÇb/‚àÇy)/‚àÇx - ‚àÇ(a * ‚àÇb/‚àÇx)/‚àÇy
    bh = fft(b)
    bx = ifft(im * grid.k .* bh)
    by = ifft(im * grid.l .* bh)
    
    return im * grid.k .* fft(a .* by) .- im * grid.l .* fft(a .* bx)
  end
end

"""
    jacobian(a, b, grid)

Return the Jacobian of `a` and `b` on `grid`.
"""
function jacobian(a, b, grid::TwoDGrid)
  if eltype(a) <: Real
    return irfft(jacobianh(a, b, grid), grid.nx)
  else
    return ifft(jacobianh(a, b, grid))
  end
end

"""
    radialspectrum(fh, grid; n=nothing, m=nothing, refinement=2)

Return the radial spectrum of `fh`. `fh` lives on Cartesian wavenumber grid ``(k, l)``. To 
compute the radial spectrum, we first interpolate ``fÃÇ(k, l)`` onto a radial wavenumber grid 
``(œÅ, Œ∏)``, where ``œÅ¬≤ = k¬≤+l¬≤`` and ``Œ∏ = \\arctan(l/k)``. Note here that 
``fÃÇ =`` `fh` ``/(`` `grid.nx` ``e^{- i ùê§ ‚ãÖ ùê±‚ÇÄ})``,  with ``ùê±‚ÇÄ`` the vector with components the 
left-most position in each direction. After interpolation, we integrate ``fÃÇ``over angles ``Œ∏`` 
to get `fœÅ`,

```math
  fÃÇ_œÅ = \\int fÃÇ(œÅ, Œ∏) œÅ ùñΩœÅ ùñΩŒ∏ \\, .
```

The resolution `(n, m)` for the polar wavenumber grid is `n = refinement * maximum(nk, nl), 
m = refinement * maximum(nk, nl)`, where `refinement = 2` by default. If `fh` is in conjugate 
symmetric form then only the upper-half plane in ``Œ∏`` is represented on the polar grid.
"""
function radialspectrum(fh, grid::TwoDGrid; n=nothing, m=nothing, refinement=2)

  n = n === nothing ? refinement * maximum([grid.nk, grid.nl]) : n
  m = m === nothing ? refinement * maximum([grid.nk, grid.nl]) : m

  # Calculate the shifted k and l
  lshift = range(-grid.nl/2, stop=grid.nl/2-1, length=grid.nl) * 2œÄ/grid.Ly

  if size(fh)[1] == grid.nkr # conjugate symmetric form
    m = Int(m/2)                         # => half resolution in Œ∏
    Œ∏ = range(-œÄ/2, stop=œÄ/2, length=m)  # Œ∏-grid from k=0 to max(kr)
    fhshift = fftshift(fh, 2)            # shifted fh
    kshift = range(0, stop=grid.nkr-1, length=grid.nkr) * 2œÄ/grid.Lx
  else # ordinary form
    Œ∏ = range(0, stop=2œÄ, length=m)      # Œ∏ grid
    fhshift = fftshift(fh, [1, 2])       # shifted fh
    kshift = range(-grid.nk/2, stop=grid.nk/2-1, length=grid.nk) * 2œÄ/grid.Lx
  end

  # Interpolator for fh
  itp = scale(interpolate(fhshift, BSpline(Linear())), kshift, lshift)

  # Get radial wavenumber vector
  œÅmax = minimum([(grid.nk/2-1) * 2œÄ/grid.Lx, (grid.nl/2-1) * 2œÄ/grid.Ly])
  œÅ = range(0, stop=œÅmax, length=n)

  # Interpolate fh onto fine grid in (œÅ, Œ∏).
  fhœÅŒ∏ = zeros(eltype(fhshift), (n, m))

  for i‚ÇÇ=1:m, i‚ÇÅ=2:n # ignore zeroth mode; i‚ÇÅ‚â•2
    fhœÅŒ∏[i‚ÇÅ, i‚ÇÇ] = itp(œÅ[i‚ÇÅ] * cos(Œ∏[i‚ÇÇ]), œÅ[i‚ÇÅ] * sin(Œ∏[i‚ÇÇ]))
  end

  # fhœÅ = œÅ ‚à´ fh(œÅ, Œ∏) dŒ∏  =>  Fh = ‚à´ fhœÅ dœÅ = ‚à´‚à´ fh dk dl
  dŒ∏ = Œ∏[2] - Œ∏[1]
  if size(fh)[1] == grid.nkr
    fhœÅ = 2œÅ .* sum(fhœÅŒ∏, dims=2) * dŒ∏ # multiply by 2 for conjugate symmetry
  else
    fhœÅ =  œÅ .* sum(fhœÅŒ∏, dims=2) * dŒ∏
  end

  CUDA.@allowscalar fhœÅ[1] = fh[1, 1] # zeroth mode

  return œÅ, fhœÅ
end

"""
    on_grid(func, grid)

Return an array, of the type compatible with the `device` of that the `grid` lives on,
that contains the values of function `func` evaluated on the `grid`.
"""
function on_grid(func, grid::OneDGrid{T}) where T
  f = zeros(grid.device, T, (grid.nx, ))

  @. f = func(grid.x)

  return f
end

function on_grid(func, grid::TwoDGrid{T}) where T
  f = zeros(grid.device, T, (grid.nx, grid.ny))

  x = reshape(grid.x, (grid.nx, 1,))
  y = reshape(grid.y, (1, grid.ny))
  
  @. f = func(x, y)

  return f
end

function on_grid(func, grid::ThreeDGrid{T}) where T
  f = zeros(grid.device, T, (grid.nx, grid.ny, grid.nz))

  x = reshape(grid.x, (grid.nx, 1, 1))
  y = reshape(grid.y, (1, grid.ny, 1))
  z = reshape(grid.z, (1, 1, grid.nz))
  
  @. f = func(x, y, z)

  return f
end

"""
    device_array(device::Device)
    device_array(device::Device, T, dim)

Return the proper array type according to the `device`, i.e., `Array` for CPU and
`CuArray` for GPU.
"""
device_array(device::CPU) = Array
device_array(device::CPU, T, dim) = Array{T, dim}
